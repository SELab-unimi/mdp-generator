grammar it.unimi.di.se.mdp.MdpDsl with org.eclipse.xtext.common.Terminals

generate mdpDsl "http://www.unimi.it/di/se/mdp/MdpDsl"

MDPModel:
    'model' name=STRING
    'actions' actions += Action*
    'states' states += State*
    'arcs' arcs += Arc*
    'observe' observableActions += ObservableMap*
    'control' controllableActions += ControllableMap*
    'reset' resetActions += ResetEvent*
    ('sampleSize' sampleSize=INT)?
    ('explorationPolicy' policy=Policy 'termination' termination=Termination)?;
  
Action:
	name=ID;  

State: // state naming convention is S<n>, n>=0
	name=ID '{' Label += AtomicProposition* '}' initial?=('initial')? Prior += DirichletPrior*;

AtomicProposition:
	name=ID;
	
DirichletPrior:
	'Dir' '~' '(' act=[Action] ',' concentration += ConcentrationParam+ ')';
	
ConcentrationParam:
	'<' dst=[State] ',' alpha=DECIMAL '>';
	
Arc:
	name=ID ':' '(' src=[State] ',' act=[Action]')' '->' dst=[State] ',' probability=DECIMAL;

Arg:
	name=ID ':' type=STRING;

ObservableMap:
	arc=[Arc] '->' signature=STRING ',' 'args' '{' Arguments += Arg+ '}' ',' 'precondition' argsCondition=STRING ',' postcondition=Postcondition;
	
Postcondition:
	'postcondition' expression=STRING 'returnType' returnType=STRING;

ControllableMap:
	state=[State] '->' signature=STRING ',' 'args' '{' Arguments += Arg+ '}'; // it assumes the presence of a single java.io.InputStream argument

ResetEvent:
	signature=STRING (',' 'args' '{' Arguments += Arg+ '}')? (',' 'argsCondition' argsCondition=STRING)?;

Policy:
	'uncertainty' | 'random' | 'history';
	
Termination:
	type=('convergence' | 'coverage' | 'limit') (coverage=DECIMAL)? (limit=INT)?;

terminal DECIMAL: INT '.' INT;
